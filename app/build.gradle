plugins {
    id "de.develappers.versioning" version "1.1.10"
}

apply plugin: 'de.develappers.versioning'

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'realm-android'
apply plugin: "androidx.navigation.safeargs"
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.gms.google-services'

android {
    compileSdkVersion 31
    defaultConfig {
        applicationId "de.htwdd.htwdresden"
        minSdkVersion 23
        targetSdkVersion 31
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        versionName versioning.versionName
        versionCode versioning.versionCode
    }

    signingConfigs {
        release
    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        debug {
            debuggable true
        }
        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    dataBinding {
        enabled = true
    }
    lint {
        abortOnError false
        checkReleaseBuilds false
        disable 'MissingTranslation'
    }
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file("keystore.properties")
if (keystorePropertiesFile.canRead()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
    android.signingConfigs.release.storeFile = file(keystoreProperties['file'])
    android.signingConfigs.release.storePassword = keystoreProperties['password']
    android.signingConfigs.release.keyAlias = keystoreProperties['alias']
    android.signingConfigs.release.keyPassword = keystoreProperties['password']
} else {
    android.buildTypes.release.signingConfig = null
}

realm {
    kotlinExtensionsEnabled = true
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    //noinspection GradleCompatible
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation "androidx.activity:activity-ktx:1.4.0"

    // https://github.com/realm/realm-android-adapters
    implementation 'io.realm:android-adapters:2.1.1'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.4.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.0'

    // https://github.com/square/retrofit
    def retrofit2_version = '2.9.0'
    implementation "com.squareup.retrofit2:converter-gson:$retrofit2_version"
    implementation "com.squareup.retrofit2:retrofit:$retrofit2_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit2_version"

    // https://square.github.io/picasso/
    implementation 'com.squareup.picasso:picasso:2.71828'

    // https://github.com/heinrichreimer/android-canteen-balance
    implementation 'com.heinrichreimersoftware:android-canteen-balance:0.1.3'

    implementation 'com.github.msayan:tutorial-view:v1.0.10'

    implementation 'com.google.firebase:firebase-core:20.0.2'
    implementation 'com.google.firebase:firebase-crashlytics:18.2.7'

    //Lottie
    implementation 'com.airbnb.android:lottie:3.4.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    // Android Kotlin Core Extensions
    implementation 'androidx.core:core-ktx:1.7.0'

    // Android Security
    def security_version = '1.1.0-alpha03'
    implementation "androidx.security:security-crypto:$security_version"

    // Android Lifecycle
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    // Android RX
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.19'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.4.0'

    // Navigation Components
    def nav_version = "2.3.5"
    implementation "androidx.navigation:navigation-fragment:$nav_version"
    implementation "androidx.navigation:navigation-ui:$nav_version"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // Material Dialogs
    def md_version = "3.1.0"
    implementation "com.afollestad.material-dialogs:core:3.3.0"
    implementation "com.afollestad.material-dialogs:input:$md_version"
    implementation "com.afollestad.material-dialogs:bottomsheets:3.3.0"
    implementation 'com.afollestad.material-dialogs:datetime:3.2.1'

    // Toasts with steroids
    def toast_version = "1.4.2"
    implementation "com.github.GrenderG:Toasty:$toast_version"

    // Konfetti
    def konfetti_verson = "1.1.3"
    implementation "nl.dionsegijn:konfetti:$konfetti_verson"

    // Zoomable ImageView
    implementation 'com.jsibbold:zoomage:1.3.0'
}
apply plugin: 'com.google.gms.google-services'
repositories {
    mavenCentral()
}